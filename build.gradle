plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.11"
}

def TEAM = 1885
def ROBOT_CLASS = "org.ilite.frc.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
	compile wpilib()
	compile ctre()
	compile navx()
	compile openrio.powerup.matchData()
	compile 'com.google.code.gson:gson:2.8.2'
	compile group: 'com.flybotix', name: 'highfrequencyrobots', version: HFRVER
	compile pathfinder()
	  
	compile name: 'eu.hansolo.fx.horizon-1.0'
	compile 'eu.hansolo:tilesfx:1.5.2'
	compile 'org.jfree:jfreechart-fx:1.0.0'
	compile 'org.controlsfx:controlsfx:9.0.0'
	
	// Poofs library and its dependencies
	compile name: 'json-simple-1.1.1'
	compile name: 'poofs.2017'
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

repositories {
	mavenCentral() // HFR, tilesFX & Medusa (gauges)
	jcenter() // fontawesomeFX
	mavenLocal()   
	flatDir {
		dirs './lib'
	}
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}